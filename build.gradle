plugins {
    id "de.undercouch.download" version "3.2.0"
}

import de.undercouch.gradle.tasks.download.Download

task downloadPromptList(type: Download) {
    src 'https://catalog.ldc.upenn.edu/docs/LDC93S1/PROMPTS.TXT'
    dest 'build/prompts.txt'
    overwrite false
}

task downloadRevealJS(type: Download) {
    src 'https://github.com/hakimel/reveal.js/archive/3.4.1.zip'
    dest 'build/reveal.js.zip'
    overwrite false
}

task downloadRevealJSPlugin(type: Download) {
    src 'https://raw.githubusercontent.com/rajgoel/reveal.js-plugins/master/audio-slideshow/audio-slideshow.js'
    dest 'build'
}

task unpackRevealJS(type: Copy) {
    dependsOn downloadRevealJS
    from zipTree('build/reveal.js.zip')
    into 'build'
    eachFile {
        it.path = it.path - '-3.4.1'
    }
}

task generateMarkdown(type: Copy) {
    dependsOn downloadPromptList
    from 'src'
    into 'build'
    include 'prompts.md', 'beep.mp3'
    ext.markdownFile = file('build/prompts.md')
    outputs.files markdownFile
    doLast {
        file('build/prompts.txt').readLines().take(500).each { line ->
            if (!line.startsWith(';')) {
                def fields = line.tokenize()
                markdownFile << "## ${fields.dropRight(1).join(' ')} {data-audio-src=\"beep.mp3\"}\n\n"
                markdownFile << "<audio data-autoplay src='beep.mp3'></audio>\n\n"
                markdownFile << "${fields.last()}\n\n"
            }
        }
    }
}

task build(type: Exec) {
    inputs.files generateMarkdown, unpackRevealJS, downloadRevealJSPlugin
    def htmlFile = file('build/prompts.html')
    outputs.files htmlFile
    commandLine 'pandoc', '-t', 'revealjs', '--standalone', generateMarkdown.markdownFile, '-o', htmlFile
    workingDir 'build'
}
